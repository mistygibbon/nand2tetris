// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    Not(in=zr , out=nzr);
    Not(in=ng , out=nng );
    And(a=nzr , b=nng , out=gt);
    And(a=gt , b=instruction[0] , out=jgt );

    And(a=zr , b=nng , out=eq);
    And(a=eq , b=instruction[1] , out=jeq );

    And(a=nzr , b=ng , out=lt);
    And(a=lt , b=instruction[2] , out=jlt );

    Or(a=jgt , b=jeq , out=jgtORjeq );
    Or(a=jgtORjeq , b=jlt , out=jgtORjeqORjlt );
    And(a=jgtORjeqORjlt , b=instruction[15] , out=loadPC );

    And(a=instruction[3] , b=instruction[15] , out=writeM );

    And16(a=Aout , b=Aout , out[0..14]=addressM );

    Not(in=instruction[15] , out=isAinst );
    Or(a=instruction[5] , b=isAinst , out=loadA );

    And(a=instruction[4] , b=instruction[15] , out=loadD );
    
    Mux16(a=instruction, b=ALUout, sel=instruction[15] , out=instMuxOut );
    Mux16(a=Aout , b=inM , sel=instruction[12] , out=memMuxOut );
    ARegister(in=instMuxOut , load=loadA , out=Aout );
    DRegister(in=ALUout , load=loadD , out=Dout );
    PC(in=Aout , load=loadPC , inc=true , reset=reset , out[0..14]=pc );
	ALU(x=Dout , y=memMuxOut , zx=instruction[11] , nx=instruction[10] , zy=instruction[9] , ny=instruction[8] , f=instruction[7] , no=instruction[6] , out=ALUout, out=outM, zr=zr , ng=ng );
}